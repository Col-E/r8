plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.android.tools'
version '8.3.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    withSourcesJar()
}

compileJava {
    // options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.encoding = 'UTF-8'
    options.incremental = true
}

ext {
    androidSupportVersion = '25.4.0'
    asmVersion = '9.5'  // When updating update tools/asmifier.py, build.src and Toolhelper as well.
    javassistVersion = '3.29.2-GA'
    espressoVersion = '3.0.0'
    fastutilVersion = '8.5.11'
    guavaVersion = '32.0.0-jre'
    joptSimpleVersion = '4.6'
    gsonVersion = '2.10.1'
    junitVersion = '4.13-beta-2'
    mockitoVersion = '2.10.0'
    // The kotlin version is only here to specify the kotlin language level,
    // all kotlin compilations are done in tests.
    kotlinVersion = '1.8.0'
    kotlinExtMetadataJVMVersion = '0.6.0'
    smaliVersion = '3.0.3'
    errorproneVersion = '2.18.0'
    testngVersion = '6.10'
}

dependencies {
    implementation "com.google.code.gson:gson:$gsonVersion"
    // Include all of guava when compiling the code, but exclude annotations that we don't
    // need from the packaging.
    compileOnly("com.google.guava:guava:$guavaVersion")
    implementation("com.google.guava:guava:$guavaVersion", {
        exclude group: 'com.google.errorprone'
        exclude group: 'com.google.code.findbugs'
        exclude group: 'com.google.j2objc'
        exclude group: 'org.codehaus.mojo'
        exclude group: 'com.google.guava', module: 'listenablefuture'
        exclude group: 'com.google.guava', module: 'failureaccess'
    })
    implementation group: 'it.unimi.dsi', name: 'fastutil', version: fastutilVersion
    implementation "org.jetbrains.kotlinx:kotlinx-metadata-jvm:$kotlinExtMetadataJVMVersion"
    implementation group: 'org.ow2.asm', name: 'asm', version: asmVersion
    implementation group: 'org.ow2.asm', name: 'asm-commons', version: asmVersion
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: asmVersion
    implementation group: 'org.ow2.asm', name: 'asm-analysis', version: asmVersion
    implementation group: 'org.ow2.asm', name: 'asm-util', version: asmVersion
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/keepanno/java']
        }
        resources {
            srcDirs "third_party/api_database/api_database"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            version = project.version
            artifactId = "r8"

            from components.java
        }
    }
}

// TODO: Configure templating plugin of some kind to update Version#MAIN_LABEL & Version#LABEL